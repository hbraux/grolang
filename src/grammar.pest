
Special = { "true" | "false" | "nil" }

Int = @{  "-"? ~ ASCII_DIGIT+  ~ ( "_" ~ ASCII_DIGIT+)* }
Float = @{ "-"? ~ ASCII_DIGIT+ ~ "." ~ ASCII_DIGIT+ ~ (^"e" ~ "-"? ~ ASCII_DIGIT+)? }

String = { "\"" ~ ( stringChar | escape )* ~ "\"" }
stringChar = _{ (!("\\" | "\"") ~ ANY) + }
escape     = _{ "\\" ~ ( "n" | "r" | "t" | "\\" | "\"" ) }

Symbol = @{ ASCII_ALPHA ~ ASCII_ALPHANUMERIC* }
RawType = @{ ":" ~ WHITESPACE* ~ Symbol ~ ( "<" ~ Symbol ~( "," ~ Symbol )? ~ ">")? }

Literal = _{ Special | Float | Int | String | RawType }

// null is only recognized in JSON expressions and filtered out
Pair = _{ Literal ~ ":" ~ ( LiteralOrCollection | "null") }

LiteralOrCollection = _{ Literal | List | Map }
List = { "[" ~ LiteralOrCollection? ~ ( "," ~ LiteralOrCollection )*  ~ "]" }
Map =  { "{" ~ Pair? ~ ( "," ~ Pair )*  ~ "}" }


Operator = _{ Add | Sub | Mul | Div | Mod | Exp | Eq | Neq | Le | Lt | Ge | Gt | And | Or }
    Add = { "+" }
    Sub = { "-" }
    Mul = { "*" }
    Div = { "/" }
    Mod = { "%" }
    Exp = { "^" }
    Eq = { "==" }
    Neq = { "!=" }
    Lt = { "<" }
    Le = { "<=" }
    Gt = { ">" }
    Ge = { ">=" }
    And = { "&&" }
    Or = { "||" }
Dot = { "." }


Expr = {  ChainCall | CallExpr | BinaryExpr  | Term  }
ChainCall = _{ Term ~ ( Dot ~ CallExpr )+  }
BinaryExpr = _{ Term ~ ( Operator ~ Term ) + }
CallExpr = { Symbol ~ "(" ~ Expr? ~ ( "," ~ Expr )* ~ ")" }
Term = _{ CallExpr | Parameters | LiteralOrCollection | Symbol |  "(" ~ Expr ~ ")"  | Block }

VarType = { "var" | "val" | "const" }
Declaration = { VarType ~ Symbol ~ RawType? ~ "=" ~ Expr }

Parameter = { Symbol ~ RawType }
Parameters = { "(" ~ Parameter? ~ ( "," ~ Parameter )* ~ ")" }
Definition = { "fun" ~ Symbol ~ Parameters ~ RawType ~ "=" ~ ( Expr | Block) }
Class = { "class" ~ Symbol ~ Parameters }
Assignment = { Symbol ~ "=" ~ Expr }

Block = { "{" ~ NEWLINE* ~ Statement ~ ( (NEWLINE | ";") ~ Statement)* ~ NEWLINE* ~ "}" }
IfElse = { "if" ~ "(" ~ Expr ~ ")" ~ ( Expr | Block) ~ ( "else" ~ ( Expr | Block))? }
While = { "while" ~ "(" ~ Expr ~ ")" ~ Block  }
Statement = _{ Declaration | Definition | Class | Assignment | IfElse | While | Expr }

equation = _{ SOI ~ Expr ~ EOI }
WHITESPACE = _{ " " }
COMMENT   = { "#" ~ (!NEWLINE ~ ANY)* ~ NEWLINE }
