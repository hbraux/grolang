WHITESPACE = _{ " " }

Special = { "true" | "false" | "null" }

Int = { ("+" | "-")? ~ ASCII_DIGIT+  ~ ( "_" ~ ASCII_DIGIT+)* }
Float = { ("+" | "-")? ~ ASCII_DIGIT+ ~ "." ~ ASCII_DIGIT* ~ (^"e" ~ ("+" | "-")? ~ ASCII_DIGIT+)? }

String = { "\"" ~ stringChar*  ~  "\"" }
stringChar = _{ !"\""  ~ ANY }

Id = { ASCII_ALPHA ~ ASCII_ALPHANUMERIC* }
Symbol = { "'" ~ Id }
Literal = _{ Special | Float | Int | Symbol | Id | String }

TypeSpec = { ":" ~ Id ~ ( "<" ~ Id ~( "," ~ Id )? ~ ">")? }

Operator = { "+" | "-" | "*" | "/" | "^" | "%" }

Expr = _{ BinaryExpr | CallExpr | Term }
BinaryExpr = { Term ~ Operator ~ Term }
CallExpr = { Term ~ "." ~ "(" ~ Expr? ~ ( "," ~ Expr )* ~ ")" }

Term = _{ Literal | "(" ~ Expr ~ ")" }

Declaration = { "var" ~ Id ~ TypeSpec? ~ "=" ~ Expr }
Assignment = { Id ~ "=" ~ Expr }

Statement = _{ Declaration | Assignment | Expr }


